_smart-sudo () {
  if [[ -n $1 ]]; then
    if [[ $1 == \-* ]]; then
      command sudo $@
    else
      local -A kprefixes
      local -a result=($@[1]) tmp
      local -i i=1
      local prevcmd

      while ; do
        while ; do
          case $result[$i] in
            nocorrect | noglob | ex | ix) kprefixes[$result[$i]]='' ;;
            *) break ;;
          esac
          ((++i))
        done

        tmp=(${(z)aliases[$result[$i]]})

        if [[ $#tmp == 0 ]] || [[ $tmp[1] == $result[$i] ]]; then
          break
        fi

        if [[ $tmp[1] == $prevcmd ]]; then
          ((--i))
          result[1,$i]=()
          break
        fi

        prevcmd=$result[1]
        result[1,$i]=($tmp)
        i=1
      done

      tmp=(${(k)kprefixes} command sudo)
      $=tmp ${==~result} "${@[2,-1]}"
    fi
  else
    #if no parameters were given, then assume we want a root shell
    command sudo -s
  fi
}
