#!/usr/bin/env zsh
__fsel() {
  if [[ -z $dirs ]]; then
    output=$(fzf $=query <<<${(F)${(f)"$(find -L ./ ! \( -name '.?*' -prune \) $=1 2> /dev/null)"}[2,-1]/??})
  else
    output=$(find -L $dirs ! \( -name '.?*' -prune \) $=1 2> /dev/null | fzf $=query)
  fi
  [[ -n $output ]] && output=${(j: :)${(qf)output}}
}

_fzf-file-or-directory() {
  local dirs query output
  if [[ "${LBUFFER[-1]}" = ' ' ]] ; then
    __fsel $1 && LBUFFER+=$output
  else
    local tail=${${(z)LBUFFER}[-1]}
    if [[ "${tail[-1]}" = '/' ]] ; then
      dirs=(${~tail})
    elif [[ -d "${tail:h}" ]] ; then
      dirs=(${~tail:h})
      query="-q ${tail:t:q}"
    else
      query="-q ${tail:q}"
    fi
    __fsel $1 && LBUFFER=${LBUFFER:0:-${#tail}}$output
  fi

  zle redisplay
  typeset -f zle-line-init >/dev/null && zle zle-line-init
}

_fzf-file-or-directory "$@"
