#!/usr/bin/env zsh
__fzf-sel() {
  output=${(j: :)${(qf)"$(fzf $=query <<<${(F)files})"}}
}

__fzf-files-check() {
  (( $#files != 0 )) && return 0
  zle -M 'not matches found'
  return 1
}

_fzf-file-or-directory() {
  local query output
  local -a files
  if [[ "${LBUFFER[-1]}" = ' ' ]] ; then
    files=(**/*(N${1}oN))
    __fzf-files-check || return 1
    __fzf-sel && LBUFFER+=$output
  else
    local found tail=${${(z)LBUFFER}[-1]}
    if [[ ${tail[-1]} = '/' ]] ; then
      files=(${~tail}**/*(N${1}oN))
      __fzf-files-check || return 1
      [[ -d $tail ]] && found=x
    else
      files=(${~tail}*/**/*(N${1}oN))
      __fzf-files-check || return 1
      found=($tail*(NoN[1]))
    fi

    if [[ -n $found ]]; then
      query="-q ${tail:q}"
    else
      local head=$tail:h
      until [[ -d $head ]]; do
        head=$head:h
      done
      if [[ $head != '.' ]]; then
        query="-q ${head:q}/"
      fi
    fi

    __fzf-sel && LBUFFER=${LBUFFER:0:-${#tail}}$output
  fi

  zle redisplay
  typeset -f zle-line-init >/dev/null && zle zle-line-init
}

_fzf-file-or-directory "$@"
