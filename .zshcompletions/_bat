#compdef bat

_bat() {
  typeset -A opt_args
  typeset -a _arguments_options
  local ret=1

  local context curcontext="$curcontext" state line
  _arguments -s -S : \
    "${_arguments_options[@]}" \
    {-l+,--language=}'[Set the language for syntax highlighting]' \
    {*-H+,*--highlight-line=}'[Highlight the given line]' \
    '--tabs=[Set the tab width to T spaces]' \
    '--wrap=[Specify the text-wrapping mode]: :(auto never character)' \
    '--terminal-width=[Explicitly set the width of the terminal instead of determining it automatically. If prefixed with '\''+'\'' or '\''-'\'', the value will be treated as an offset to the actual terminal width. See also: '\''--wrap'\'']' \
    '--color=[When to use colors]: :(auto never always)' \
    '--italic-text=[Use italics in output]: :(always never)' \
    '--decorations=[When to show the decorations]: :(auto never always)' \
    '--paging=[Specify when to use the pager]: :(auto never always)' \
    '--pager=[Determine which pager to use]:pager: _command_names -e' \
    {*-m+,*-map-syntax=}'[Map a file extension or name to an existing syntax]' \
    '*--theme=[Set the color theme for syntax highlighting]: :_bat_themes' \
    '--style=[Comma-separated list of style elements to display]: : _values -s , style auto full plain changes header grid numbers snip ' \
    {*-r+,*--line-range=}'[Only print the lines from N to M]' \
    '(-l --language)'{-A,--show-all}'[Show non-printable characters (space, tab, newline, ..)]' \
    {*-p,*--plain}'[Show plain style (alias for '\''--style=plain'\'')]' \
    {-n,--number}'[Show line numbers (alias for '\''--style=numbers'\'')]' \
    '--list-themes[Display all supported highlighting themes]' \
    '(--list-themes)'{-L,--list-languages}'[Display all supported languages]' \
    {-u,--unbuffered}'[POSIX-compliant unbuffered output. Option is ignored]' \
    '--file-name[Specify the name to display for a file]' \
    {-d,--diff}'[Only show lines that have been added/removed/modified with respect to the Git index]' \
    '--diff-context=[Include N lines of context around added/removed/modified lines when using "--diff"]:N' \
    '--no-config[Do not use the configuration file]' \
    '(-L --list-languages --list-themes)--config-file[Show path to the configuration file]' \
    '--config-dir[Show bat'\''s configuration directory]' \
    '--cache-dir[Show bat'\''s cache directory]' \
    {-h,--help}'[Print this help message]' \
    {-V,--version}'[Show version information]' \
    '*: :_files' \
    && ret=0
}

_bat_themes() {
  local -a themes
  themes=(${(f)"$(_call_program themes bat --list-themes)"})
  _describe -t themes 'themes' themes
}

_bat "$@"
